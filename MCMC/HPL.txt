model {
# LIKELIHOOD AND RANDOM EFFECT MODEL FOR THE SCORING PROPENSITY
for (g in 1:ngames) {

# Observed number of expected goals scored by each team
y.home[g] ~ dpois(theta[g,1])
y.away[g] ~ dpois(theta[g,2])

# Predictive distribution for the number of expected goals
y.pred[g,1] ~ dpois(theta[g,1])
y.pred[g,2] ~ dpois(theta[g,2])

# Average Scoring intensities (accounting for mixing components)
log(theta[g,1]) <- home + att[team_home_index[g]] + def[team_away_index[g]]
log(theta[g,2]) <- att[team_away_index[g]] + def[team_home_index[g]]
}

# 1. BASIC MODEL FOR THE HYPERPARAMETERS

# prior on the home effect
home ~ dnorm(0,0.0001)

# Trick to code the ‘‘sum-to-zero’’ constraint
for (t in 1:nteams){
att.star[t] ~ dnorm(mu.att,tau.att)
def.star[t] ~ dnorm(mu.def,tau.def)
att[t] <- att.star[t] - mean(att.star[])
def[t] <- def.star[t] - mean(def.star[])
}

# priors on the random effects
mu.att ~ dnorm(0,0.0001)
mu.def ~ dnorm(0,0.0001)
tau.att ~ dgamma(.01,.01)
tau.def ~ dgamma(.01,.01)